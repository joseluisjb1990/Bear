Programa -> Definiciones 'oso' '(' ')' '=>' 'extinto' ListaInstrucciones

ListaInstrucciones -> Instruccion ';'
                   |  ListaInstrucciones Instruccion ';'

Instruccion -> ListaIdentificadores '=' ListaExpresiones
            |  'leer' '(' ID ')'
            |  'escribir' '(' Expresion ')'
            |  Funcion
            |  'si' ExpresionBooleana 'entonces' '{' ListaInstrucciones '}'
            |  'si' ExpresionBooleana 'entonces' '{' ListaInstrucciones '}' sino '{' ListaInstrucciones '}'
            |  'para' ID 'en' '(' Expresion ',' Expresion ')' '{' ListaInstrucciones '}'
            |  'para' ID 'en' '(' Expresion ',' Expresion ',' Expresion ')' '{' ListaInstrucciones '}'
            |  'para' ID 'en' ID '{' ListaInstrucciones '}'
            |  IteracionIndeterminada
            |  ID '++'
            |  ID '--'
            |  'vomita'
            |  'vomita' ID
            |  'fondoBlanco'
            |  'fondoBlanco' ID
            |  'roloePea'
            |  'roloePea' ID

Expresion -> POLAR
          |  KODIAK
          |  HORMIGUERO
          |  MALAYO
          |  ID
          |  Funcion
          |  FuncionPredef
          |  Cueva
          |  ID '->' Expresion
          |  ID '.' Expresion
          |  ExpresionBooleana
          |  ExpresionAritmetica
          |  ExpresionBooleana '?' Expresion ':' Expresion

ExpresionAritmetica -> Expresion '+' Expresion
                    |  Expresion '-' Expresion
                    |  Expresion '**' Expresion
                    |  Expresion '*' Expresion
                    |  Expresion '/' Expresion
                    |  '-' Expresion

ExpresionBooleana -> BLANCO
                  |  NEGRO
                  |  Expresion   '<'   Expresion
                  |  Expresion   '<='  Expresion
                  |  Expresion   '>'   Expresion
                  |  Expresion   '>='  Expresion
                  |  Expresion   '=='  Expresion
                  |  Expresion   '=/=' Expresion
                  |  Expresion   '|'   Expresion
                  |  Expresion   '&'   Expresion
                  |  'no' Expresion

Funcion -> ID '(' Parametros ')'

Parametros -> ListaParametros
           |  /* Vacio *\

ListaParametros -> Parametro
                |  ListaParametros ',' Parametro

Parametro -> ID
          |  Expresion

FuncionPredef -> 'a_panda'  '(' Expresion ')'
              |  'a_polar'  '(' Expresion ')'
              |  'a_malayo' '(' Expresion ')'
              |  'a_polar'  '(' Expresion ')'
              |  'lon'      '(' Expresion ')'

Cueva -> ID '[' NUM ']'
      |  Cueva '[' NUM ']'

IteracionIndeterminada -> ID ':' 'mientras' '(' ExpresionBooleana ')' '{' ListaInstrucciones '}'
                       |  'mientras' '(' ExpresionBooleana ')' '{' ListaInstrucciones '}'

Definiciones -> ListaDefGlobales
             |  /* Vacio *\

ListaDefGlobales -> DefinicionGlobal ';'
                 |  ListaDefGlobales DefinicionGlobal ';'

DefinicionGlobal -> DefConstante
                 |  DefVariable
                 |  DefFuncion

DefConstante -> 'const' Tipo ListaIdentificadores '=' ListaExpresiones

ListaIdentificadores -> ID
                     |  ListaIdentificadores ',' ID

ListaExpresiones -> Expresion
                 |  ListaExpresiones ',' Expresion

DefVariable -> Tipo ListaIdentificadores '=' ListaExpresiones
            |  Tipo ListaIdentificadores
            |  DefCueva
            |  DefCompleja

DefCueva -> Cuevas Tipo ID

Cuevas -> 'cueva' '[' NUM ']' 'de'
       |  Cuevas 'cueva' '[' NUM ']' 'de'

DefCompleja -> 'pardo'   ID '{' Campos '}'
            |  'grizzli' ID '{' Campos '}'

Campos -> Tipo ID ';'
       |  Campos Tipo ID ';'

DefFuncion -> ID '(' DefParametros ')' '=>' Tipo
           |  ID '(' DefParametros ')' '=>' Tipo '{' ListaInstrucciones '}'

DefParametros -> DefParametro
              |  DefParametros ',' DefParametro

DefParametro -> Tipo ID
          |  '^' Tipo ID
          |  ParametroCueva Tipo ID

ParametroCueva -> 'cueva' '[' ']' 'de'
               |  ParametroCueva 'cueva' '[' NUM ']' 'de'

Tipo -> ID
     |  'panda'
     |  'polar'
     |  'kodiak'
     |  'malayo'
     |  'hormiguero'
     |  'extinto'
